plugins {
  id 'org.xtext.xtend' version '2.0.5'
  id 'org.xtext.builder' version '2.0.5'
  id 'java'
  id 'eclipse'
  id 'application'
  id 'maven'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def blangVersion = '4.0.1' 
dependencies {
  xtextLanguages "com.github.UBC-Stat-ML.blangDSL:ca.ubc.stat.blang:${blangVersion}"
  compile group: 'com.github.UBC-Stat-ML', name: 'blangSDK', version: '2.18.0'
  compile group: 'com.github.UBC-Stat-ML', name: 'nowellpack', version: '0.0.0'
  compile group: 'com.github.UBC-Stat-ML', name: 'conifer', version: '2.1.3'
}

xtext {
  languages {
    blang {
      setup = 'ca.ubc.stat.blang.BlangDslStandaloneSetup'
      fileExtension = 'bl'
      generator.outlet.producesJava = true
      generator.javaSourceLevel = '1.8'
    }
  }
}

repositories {
  mavenCentral()
  jcenter()
  maven { url "file:///${System.properties['user.home']}/artifacts/" }
  maven { url 'https://jitpack.io' }
  maven { url "https://ubc-stat-ml.github.io/artifacts/" }
  maven { url "https://www.stat.ubc.ca/~bouchard/maven/" }
}

xtend {
  generator {
    javaSourceLevel = 1.8
  }
}

mainClassName = "demos.Main"

configurations.archives.with {
  artifacts.remove artifacts.find { it.file =~ 'tar' }
  artifacts.remove artifacts.find { it.file =~ 'zip' }
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file:///${System.properties['user.home']}/artifacts/")
      snapshotRepository(url: "file:///${System.properties['user.home']}/artifacts/")
      pom.version = "1.0.0" /* blang-standalone-compiler-directive - do not edit : EXTRACT_VERSION */
      pom.artifactId = "blangDemos"
      pom.groupId = "com.github.UBC-Stat-ML"
    }
  }
}

jar {
    from { fileTree(dir: 'src/main/java', includes: [ '**/*.bl' ]) }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}
